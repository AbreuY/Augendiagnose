package fxml

import de.eisfeldj.augendiagnosefx.fxelements.SizeableImageView
import de.eisfeldj.augendiagnosefx.controller.DisplayImageController
import javafx.scene.control.Button
import javafx.scene.control.Label
import javafx.scene.control.ToggleButton
import javafx.scene.control.TextArea
import javafx.scene.layout.GridPane
import javafx.scene.layout.ColumnConstraints
import javafx.scene.layout.RowConstraints

component DisplayImage controlledby DisplayImageController resourcefile "../bundles/Strings.properties" styledwith "../css/application.css" {
	GridPane id displayImage {
		styleClass : "displayImage",
		alignment : "CENTER",

		rowConstraints : [
			RowConstraints {
				vgrow : "SOMETIMES"
			},
			RowConstraints id commentConstraints {
				vgrow : "SOMETIMES",
				percentHeight : 20
			}
		],
		columnConstraints : [
			ColumnConstraints {
				hgrow : "SOMETIMES"
			}
		],
		SizeableImageView id displayImageView {
			static columnIndex : 0,
			static rowIndex : 0,
			static halignment : "CENTER",
			static valignment : "CENTER"
		},
		GridPane id commentPane {
			static columnIndex : 0,
			static rowIndex : 1,
			columnConstraints : [
				ColumnConstraints {
					hgrow : "NEVER"
				},
				ColumnConstraints {
					hgrow: "SOMETIMES"
				}
			],
			Label {
				styleClass : "labelComment",
				text : rstring "label_comment",
				static columnIndex : 0,
				static rowIndex : 0,
				static valignment : "TOP"
			},
			TextArea id txtImageComment {
				promptText : rstring "hint_edit_comment",
				editable : false,
				static columnIndex : 1,
				static rowIndex : 0,
				static rowSpan : 2,
				prefWidth : 0
			},
			ToggleButton id btnEditComment {
				text : rstring "button_edit_comment",
				selected : false,
				static columnIndex : 0,
				static rowIndex : 1,
				static valignment : "BOTTOM",
				onAction : controllermethod btnEditCommentPressed
			}
		}
	}
}
