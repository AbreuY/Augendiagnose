package fxml

import de.eisfeldj.augendiagnosefx.controller.PreferencesController
import javafx.collections.FXCollections
import javafx.scene.control.Button
import javafx.scene.control.CheckBox
import javafx.scene.control.ChoiceBox
import javafx.scene.control.ColorPicker
import javafx.scene.control.Label
import javafx.scene.control.TextField
import javafx.scene.image.Image
import javafx.scene.image.ImageView
import javafx.scene.layout.ColumnConstraints
import javafx.scene.layout.GridPane

component Settings controlledby PreferencesController resourcefile "../bundles/Strings.properties" styledwith "../css/application.css" {
	GridPane id settingsPane {
		styleClass : "display",
		columnConstraints : ColumnConstraints {
		},
		columnConstraints : ColumnConstraints {
			hgrow : "SOMETIMES"
		},
		children : [
			Label {
				text : rstring "title_settings",
				styleClass : "settingsHeading",
				static columnIndex : 0,
				static rowIndex : 0,
				static columnSpan : 3
			},

			Label {
				text : rstring "pref_title_folder_photos",
				static columnIndex : 0,
				static rowIndex : 1
			},

			TextField id textFolderPhotos {
				minWidth : 300,
				static columnIndex : 1,
				static rowIndex : 1
			},

			Button {
				static columnIndex : 2,
				static rowIndex : 1,
				styleClass : "imageButton",
				graphic : ImageView {
					image : Image {
						url : location "img/folder.png"
					}
				},
				onAction : controllermethod selectDirectory
			},

			Label {
				text : rstring "pref_title_max_bitmap_size",
				static columnIndex : 0,
				static rowIndex : 2
			},

			ChoiceBox<Integer> id choiceMaxBitmapSize {
				static columnIndex : 1,
				static rowIndex : 2,
				static columnSpan : 2,
				items : FXCollections createdby observableArrayList {
					Integer(4096), Integer(2880), Integer(2048), Integer(1440), Integer(1024)
				}
			},

			Label {
				text : rstring "pref_title_thumbnail_size",
				static columnIndex : 0,
				static rowIndex : 3
			},

			ChoiceBox<Integer> id choiceThumbnailSize {
				static columnIndex : 1,
				static rowIndex : 3,
				static columnSpan : 2,
				items : FXCollections createdby observableArrayList {
					Integer(2048), Integer(1440), Integer(1024), Integer(720), Integer(512)
				}
			},

			Label {
				text : rstring "pref_title_overlay_color",
				static columnIndex : 0,
				static rowIndex : 4
			},

			ColorPicker id colorPicker {
				static columnIndex : 1,
				static rowIndex : 4,
				static columnSpan : 2
			},

			Label {
				text : rstring "pref_title_sort_by_last_name",
				static columnIndex : 0,
				static rowIndex : 5
			},

			CheckBox id checkBoxSortByLastName {
				static columnIndex : 1,
				static rowIndex : 5,
				static columnSpan : 2
			},

			Button {
				text : rstring "button_cancel",
				static columnIndex : 0,
				static rowIndex : 6,
				static halignment : "LEFT",
				onAction : controllermethod cancel
			},

			Button {
				text : rstring "button_ok",
				static columnIndex : 1,
				static rowIndex : 6,
				static columnSpan : 2,
				static halignment : "RIGHT",
				onAction : controllermethod submit
			}
		]
	}
}
